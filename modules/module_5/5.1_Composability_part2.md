# 5.1 Composability (Part 2): Signer vs Caller

Our source file for this program, `quadratic/src/main.leo` will contain the following content:

```leo
import arithmetic.aleo;

program quadratic.aleo {
    transition residue(a: u64, n: u64) -> u64 {
        let squared: u64 = arithmetic.aleo/square_u64(a);
        return squared.mod(n);
    }
}
```

First, note the line at the top, outside the program code block.

```leo
import arithmetic.aleo;
```

Then, inside the `residue` transition, note the external call:

```leo
let squared: u64 = arithmetic.aleo/square_u64(a);
```

As you can see, external transition calls have almost the same syntax as calling an internal function. The difference is that the function identifier includes the ID of the imported program followed by the function name, separated by a forward slash.

On Aleo, there is no "call by address", as you would do in Solidity, at the time this course is written. This means it's not possible to dynamically depend on any arbitrary program implementing a certain interface, and then provide that program address as one of the arguments to the calling function.

## Signer vs Caller

In a transition, you can get the address of the direct caller of the transition, whether it's a program address or a user account address.

```leo
transition get_caller() -> address {
    let direct_caller: address = self.caller;
    return direct_caller;
}
```

For example, for the following call chain: `User → Program A → Program B`

- `self.caller` in program B would be the address of program A.
- `self.signer` in program B would be the user's account address.

Here is an example. The `direct_calls_only` function defined below could only be called directly by a user and would fail otherwise if called from an importing program.

```leo
transition direct_calls_only() {
    let direct_caller: address = self.caller;
    let transaction_signer: address = self.signer;
    assert_eq(direct_caller, transaction_signer);
}
```

You can directly use a program ID in Leo to get its address, for example, to get the address of `my_program.aleo`:

```leo
let my_program_address: address = my_program.aleo;
``` 